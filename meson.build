project(
        'c-rbtree',
        'c',
        default_options: [
                'c_std=c11'
        ],
        license: 'Apache',
        meson_version: '>=0.60.0',
        version: '3.0.0',
)
major = meson.project_version().split('.')[0]
project_description = 'Intrusive Red-Black Tree Collection'

mod_pkgconfig = import('pkgconfig')
use_ptrace = get_option('ptrace')

dep_cstdaux = dependency('libcstdaux-1')
add_project_arguments(dep_cstdaux.get_variable('cflags').split(' '), language: 'c')

use_noundefined = meson.get_compiler('c').has_link_argument('-Wl,--no-undefined')

subdir('src')

meson.override_dependency('libcrbtree-'+major, libcrbtree_dep, static: true)
